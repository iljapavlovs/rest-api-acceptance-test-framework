buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
}

plugins {
    id "idea"
    id "com.github.kt3k.coveralls" version "2.8.2"
    id "com.jfrog.bintray" version "1.8.4" apply false
}

apply from: "$rootDir/gradle/dependencies.gradle"
apply from: "$rootDir/gradle/wrapper.gradle"

idea {
    project {
        languageLevel = "1.8"
    }
}

defaultTasks "clean", "build"

allprojects {
    apply plugin: 'com.github.kt3k.coveralls'
    apply plugin: 'jacoco'

    repositories {
        mavenCentral()
        jcenter()
    }
}

repositories {
    mavenCentral()
    jcenter()
}

subprojects {
    apply plugin: "java"
    apply plugin: "maven-publish"
    apply from: "$rootDir/gradle/publishing.gradle"
    apply from: "$rootDir/gradle/bintray.gradle"

    sourceCompatibility = 1.8

    jar {
        version = projectVersion
    }

    jacocoTestReport {
        reports {
            xml.enabled = true // coveralls plugin depends on xml format report
            html.enabled = true
        }
    }
}

task jacocoRootReport(type: JacocoReport) {
    dependsOn = subprojects.test
    sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories =  files(subprojects.sourceSets.main.output)
    executionData = files(subprojects.jacocoTestReport.executionData)
    reports {
        html.enabled = true // human readable
        xml.enabled = true // required by coveralls
    }
}

coveralls {
    sourceDirs = subprojects.sourceSets.main.allSource.srcDirs.flatten()
    jacocoReportPath = "${buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
}